/**********************************************************************
 Java CUP specification for a parser for egg programs
 **********************************************************************/

import java_cup.runtime.*;
import java.util.*;

/* The code below redefines method syntax_error to give better error messages
 * than just "Syntax error"
 */
parser code {:

public void syntax_error(Symbol currToken) {
    if (currToken.value == null) {
        ErrMsg.fatal(0,0, "Syntax error at end of file");
    }
    else {
        ErrMsg.fatal(((TokenVal)currToken.value).linenum,
                     ((TokenVal)currToken.value).charnum,
                     "Syntax error");
    }
    System.exit(-1);
}
:};


/* Terminals (tokens returned by the scanner) */
terminal                INT;
terminal                BOOL;
terminal                VOID;
terminal                TRUE;
terminal                FALSE;
terminal                STRUCT;
terminal                CIN;
terminal                COUT;
terminal                IF;
terminal                ELSE;
terminal                WHILE;
terminal		REPEAT;
terminal                RETURN;
terminal IdTokenVal     ID;
terminal IntLitTokenVal INTLITERAL;
terminal StrLitTokenVal STRINGLITERAL;
terminal                LCURLY;
terminal                RCURLY;
terminal                LPAREN;
terminal                RPAREN;
terminal                SEMICOLON;
terminal                COMMA;
terminal                DOT;
terminal                WRITE;
terminal                READ;
terminal                PLUSPLUS;
terminal                MINUSMINUS;
terminal                PLUS;
terminal                MINUS;
terminal                TIMES;
terminal                DIVIDE;
terminal                NOT;
terminal                AND;
terminal                OR;
terminal                EQUALS;
terminal                NOTEQUALS;
terminal                LESS;
terminal                GREATER;
terminal                LESSEQ;
terminal                GREATEREQ;
terminal                ASSIGN;


/* Nonterminals
 *
 * NOTE: You will need to add more nonterminals to this list as you
 *       add productions to the grammar below.
 */
non terminal ProgramNode      program;
non terminal LinkedList       declList;
non terminal DeclNode         decl;
non terminal VarDeclNode      varDecl;
non terminal TypeNode         type;
non terminal IdNode           id;
non terminal StructDeclNode   structDecl;
non terminal LinkedList<DeclNode>     structBody;
non terminal FnDeclNode       fnDecl;
non terminal FnBodyNode       fnBody;
non terminal LinkedList<DeclNode>     varDeclList;
non terminal StmtNode         stmt;
non terminal LinkedList<StmtNode>     stmtList;
non terminal FormalsListNode  formals;
non terminal LinkedList<FormalDeclNode> formalsList;
non terminal FormalDeclNode   formalDecl;
non terminal LinkedList<ExpNode> actualList;
non terminal CallExpNode      fncall;
non terminal ExpNode          loc;
non terminal AssignNode       assignExp;
non terminal ExpNode          exp;
non terminal ExpNode          term;

 
/* NOTE: Add precedence and associativity declarations here */

precedence right ASSIGN;
precedence nonassoc OR;
precedence nonassoc AND;
precedence left EQUALS, NOTEQUALS, LESS, GREATER, LESSEQ, GREATEREQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left NOT;

start with program;



/* Grammar with actions
 *
 * NOTE: add more grammar rules below
 */
program         ::= declList: d
                {: RESULT = new ProgramNode(new DeclListNode(d));
                :}
                ;

declList        ::= declList:dl decl:d
                {: dl.addLast(d);
                   RESULT = dl;
                :}
                | /* epsilon */
                {: RESULT = new LinkedList<DeclNode>();
                :}
                ;

decl            ::= varDecl:v
                {: RESULT = v;
                System.out.println("decl" + v);
                :}
                | fnDecl:f
                {:
                RESULT = f;
                :}
                | structDecl:sd     // struct definitions only at top level
                {:
                RESULT = sd;
                :}
                ;

varDecl         ::= type:t id:i SEMICOLON
                {:
                RESULT = new VarDeclNode(t, i, VarDeclNode.NOT_STRUCT);
                :}
                | STRUCT id:typeId id:iden SEMICOLON
                {:
                RESULT = new VarDeclNode(new StructNode(typeId), iden, 0);
                :}
                ;
                
fnDecl          ::= type:t id:i formals:f fnBody:fb
                {:
                RESULT = new FnDeclNode(t, i, f, fb);
                :}
                ;

fnBody          ::= LCURLY varDeclList:vList stmtList:sList RCURLY
                {:
                RESULT = new FnBodyNode(new DeclListNode(vList), new StmtListNode(sList));
                :}
                ;
                
formals         ::= LPAREN RPAREN
                {:
                RESULT = new FormalsListNode(new LinkedList<FormalDeclNode>());
                :}
                | LPAREN formalsList:fList RPAREN
                {:
                RESULT = new FormalsListNode(fList);
                :}
                ;

formalsList     ::= formalDecl:f
                {:
                LinkedList<FormalDeclNode> fList = new LinkedList<FormalDeclNode>();
                fList.addFirst(f);
                RESULT = fList;
                :}
                | formalDecl:f COMMA formalsList:fList
                {:
                fList.addFirst(f);
                RESULT = fList;
                :}
                ;

formalDecl      ::= type:t id:i        // note: no struct parameters
                {:
                RESULT = new FormalDeclNode(t, i);
                :}
                ;
                
varDeclList     ::= varDeclList:vList varDecl:v
                {:
                vList.addLast(v);
                RESULT = vList;
                :}
                | /* epsilon */
                {:
                RESULT = new LinkedList<DeclNode>();
                :}
                ;
                

stmtList        ::= stmtList:sList stmt:s
                {:
                System.out.println("stmtlist");
                sList.addLast(s);
                RESULT = sList;
                :}
                | /* epsilon */
                {:
                RESULT = new LinkedList<StmtNode>();
                :}
                ;
                
type            ::= INT
                {: RESULT = new IntNode();
                :}
                | BOOL
                {: RESULT = new BoolNode();
                :}
                | VOID
                {: RESULT = new VoidNode();
                :}
                ;
                
structDecl      ::= STRUCT id:iden LCURLY structBody:sb RCURLY SEMICOLON
                {:
                DeclListNode sbNode = new DeclListNode(sb);
                RESULT = new StructDeclNode(iden, sbNode);
                :}
                ;

structBody      ::= structBody:sb varDecl:v
                {:
                sb.addLast(v);
                RESULT = sb;
                :}
                | varDecl:v
                {:
                LinkedList<DeclNode> sbList = new LinkedList<DeclNode>();
                sbList.addLast(v);
                RESULT = sbList;
                :}
                ;
                
stmt            ::= assignExp:ae SEMICOLON
                {:
                System.out.println("in stmt");
                RESULT = new AssignStmtNode(ae);
                :}
                ;
                
assignExp       ::= loc:l ASSIGN exp:e
                {:
                System.out.println("in assignexp");
                RESULT = new AssignNode(l, e);
                :}
                ;
                
exp             ::= term:t
                {:
                RESULT = t;
                :}
                ;
                
term            ::= loc:l
                {:
                RESULT = l;
                :}
                | INTLITERAL:f
                {:
                RESULT = new IntLitNode(f.linenum, f.charnum, f.intVal);
                :}
                | STRINGLITERAL:f
                {:
                RESULT = new StringLitNode(f.linenum, f.charnum, f.strVal);
                :}
                | TRUE:f
                {:
                RESULT = new TrueNode(0, 0);
                //Fix me??
                :}
                | FALSE:f
                {:
                RESULT = new FalseNode(0, 0);
                //Fix me??
                :}
                | LPAREN exp:e RPAREN
                {:
                RESULT = e;
                :}
                | fncall:f
                {:
                RESULT = f;
                :}
                ;
                
fncall          ::=  id:i LPAREN RPAREN   // fn call with no args
                {:
                RESULT = new CallExpNode(i);
                :}
                | id:i LPAREN actualList:aList RPAREN  // with args
                {:
                RESULT = new CallExpNode(i, new ExpListNode(aList));
                :}
                ;
                
actualList      ::= exp:e
                {:
                LinkedList<ExpNode> aList = new LinkedList<ExpNode>();
                aList.addLast(e);
                RESULT = aList;
                :}
                | actualList:aList COMMA exp:e
                {:
                aList.addLast(e);
                RESULT = aList;
                :}
                ;
                                                
loc             ::= id:i
                {:
                RESULT = i;
                :}
                | loc:l DOT id:i
                {:
                RESULT = new DotAccessExpNode(l, i);
                :}
                ;

id              ::= ID:i
                {: RESULT = new IdNode(i.linenum, i.charnum, i.idVal);
                :}
                ;
				
